// Copyright (C) 2014 OneJS
//
//   Licensed under the Apache License, Version 2.0 (the "License");
//   you may not use this file except in compliance with the License.
//   You may obtain a copy of the License at
//       http://www.apache.org/licenses/LICENSE-2.0
//   Unless required by applicable law or agreed to in writing, software
//   distributed under the License is distributed on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//   See the License for the specific language governing permissions and
//   limitations under the License.

import gl
import math
import constraint 
import input
import text_tagged

class Scene extends Device{

	// its asynchronous hittesting.
	m_camera = mat4()

	signal test

	hit_capture = 0

	hitTest2D(x, y){
		// worker
		return callHost('hitTest2D', x, y)
	}

	$hitTest2D:(x, y){
		test = 10
		// main thread
		vec3 pick( (x / frame.w) * 2 - 1, -( y / frame.h) * 2 + 1, -1)

		ray3 ray()
		ray.origin = pick * mat4 m_unproj
		if(perspective){
			ray.direction = vec3(0, 0, -1) * mat4 m_view
		}
		else{
			ray.direction = vec3(0, 0, -1) * mat4 m_view
		}

		var results = []

		if(hit_capture){
			hit_capture.hitTest(ray, results, true)
		}
		else for(var child from children){
			child.hitTest(ray, results)
		}
		return results
	}

	// allright so lets solve our layout..
	m_view = mat4.identity()//mat4.RT(0,0,0, 0,0,3)
	m_unproj = mat4.identity()

	draw:(parent, sec){
		vec3 f = fill
		clearAll(f)
		//h = frame.w * 0.5 
		//h = frame.h
		if(perspective){
			m_proj = mat4.perspective(perspective, frame.w / frame.h, -1, 1)			
		}
		else{
			m_proj = mat4.ortho(0, frame.w / frame.ratio, 0, frame.h / frame.ratio, -1000, 1000)
		}
		m_unproj = mat4.invert(m_proj)
		//m_proj = mat4.perspective(-90deg, -1, -1000, 1000) * mat4.S(0.01,0.01,0.01)
		// silently assign camera matrix
		m_uncamera = mat4 m_view * mat4 m_unproj
		__m_camera = mat4 m_view * mat4 m_proj 
		m_childworld = mat4.identity()
		drawChildren(this, sec) 
	}
}

// a layer with matrices
class Layer extends Draw{

	// define object types
	mesh = vec2Rect(0, 0, 1, 1)

	// type them, otherwise we cant use it in the compiler
	m_world = mat4()
	m_mesh = mat4.identity()
	m_child = mat4.identity()

	vertex: mesh[] * (m_mesh * m_world * device.m_camera)

	p: pixel mesh[]
	hp: 2.*(pixel mesh[] -.5)

	beforeMatrix:(){
	}

	drawSelf:(sec){
		this{}
	}
		
	bounding_box = box3(0,0,0, 1,1,1)
	infinity_box = box3(-1000000, -1000000,0,1000000,1000000,1)

	findHit(hits){
		for(var hit from hits){
			if(hit.layer == this) return hit
		}
		return undefined
	}

	$hitTest:(ray3 ray, results, captured){
		// lets move to our mesh space
		var to_mesh = mat4.invert(mat4 m_mesh * mat4 m_world)
		var my_ray = ray3.transform(ray, to_mesh)

		// we can do a box intersection
		if(captured){
			results.push({layer:this, where: my_ray.intersectBox(infinity_box)})
			return true
		}

		var where = my_ray.intersectBox(bounding_box)
		if(where){
			results.unshift({layer:this, where:where})
			if(children) for(var child from children){
				child.hitTest(ray, results)
			}
			return true
		}
	}

	draw:(parent, sec){
		drawn = true
		mat4 m_parent = parent.m_childworld
		// set our matrix
		beforeMatrix()
		var dx = ax*w, dy = ay*h, dz = az*d
		mat4 m_local = mat4.TSRT(-dx,-dy,-dz ,sx,sy,sz, rx,ry,rz, x+dx,y+dy,z+dz)
		// scale our mesh
		m_mesh = mat4.S(w, h, d)

		// generate our world matrix
		m_world = m_local * m_parent

		// draw ourselves
		drawSelf(sec)

		// now lets make these things work
		if(children){
			//console.log(x,y,__proxy__,w,h)
			m_childworld = m_child * m_world
			if(!skip_children)drawChildren(this, sec)
		}
	}
	// anchor
	ax = 0.5
	ay = 0.5
	az = 0
	// rotation
	rx = 0
	ry = 0
	rz = 0
	// scale
	sx = 1
	sy = 1
	sz = 1
	// coordinates
	x = 0
	y = 0
	z = 0
	// size
	w = 100
	h = 100
	d = 1
}

class Text extends Layer{
	load(TaggedTextImpl)

	constructor(deep_outer){
		Layer::constructor(deep_outer)
		TaggedTextImpl::constructor()
	}
	vertex: (mesh[].xy+vec2(text_x, text_y)) * (m_world * device.m_camera)
	font_size = 20
	u_boldness= 0.8
	u_color = #white
}

class DebugLayerImpl{
	constructor(){
		debug_mesh = vec3[]()
	}
	
	clearDebug(){
		debug_mesh.length = 0
	}

	addDebug(x,y,w = 2,h = 2,col = 0){
		vec3[] mesh = debug_mesh
		var o = mesh.quad.length++
		mesh.quad_tl[o] = vec2(x,y)
		mesh.quad_tr[o] = vec2(x+w,y)
		mesh.quad_bl[o] = vec2(x,y+h)
		mesh.quad_br[o] = vec2(x+w,y+h)
		mesh.quad[o].z = col
	}

	debug_vertex: debug_mesh[].xy * (m_world * device.m_camera)
	debug_pixel:(){
		int dbg = debug_mesh[].z
		if(dbg == 0) return #red
		if(dbg == 1) return #green
		if(dbg == 2) return #blue
		return #orange
	}
}



	//clipboard = Clipboard.new()
	//mouse = Mouse.new()
	//key = Key.new()
	//midi = Midi.new()
/*
	// set up a constraint solver
	solver = CS.SimplexSolver.new()

	// when we are adding a node we get called
	onAddChild(node){
		// lets process some constraints
		var proto = node
		while(proto && proto.__constraints__){
			if(proto.hasOwnProperty('__constraints__')){
				for(var c from proto.__constraints__){
					var constraint = CS.ASTNodeToConstraint(node, c)
					var storage = node.__constraint_store__ || (node.__constraint_store__ = [])
					storage.push(constraint)
					solver.addConstraint(constraint)
				}
			}
			proto = Object.getPrototypeOf(proto)
		}
		if(!layout_cb) layout_cb = setImmediate(onLayout.bind(this))
	}

	onLayout(){
		layout_cb = undefined
		solver.solve()
		// alright so.. how do we get those pesky values to update?
		redraw()
	}
*/